buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.github.alessandroscarlatti'
version '0.2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "ch.qos.logback:logback-core:1.2.3"
    compile "org.slf4j:slf4j-api:1.7.28"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    archiveName = "ContextMenuHelper.jar"
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes([
                "Main-Class": "io.github.alessandroscarlatti.WindowsContextMenuHelper"
        ])
    }
}

task buildDistDir {
    group = "distribution"
    description = "Build the distribution .zip file"
    doLast {
        // assemble the example dist
        // start from an empty dist dir
        def projectBaseDir = file("MenuProjectBase")
        mkdir(projectBaseDir)

        copy {
            from file("Project")
            from file("build/libs/ContextMenuHelper.jar")
            into projectBaseDir
        }
    }
}

task buildDistZip(type: Zip) {
    group = "distribution"
    description = "Build the distribution .zip file"
    dependsOn buildDistDir
    // assemble the example dist zip
    from file("MenuProjectBase")
    into "MenuProjectBase"
    archiveName = "ContextMenuHelper.zip"
    destinationDir = file("build/distributions")
}

// build the dist after building the jar
build.finalizedBy(buildDistZip)